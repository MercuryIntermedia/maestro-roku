import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/core/Tasks.bs"
import "pkg:/source/core/MIOC.bs"
import "pkg:/source/view/BaseView.bs"

namespace mv
  ' /**
  '  * @module BaseScreen
  '  * @description Class based counterpart to BaseView component
  '  *              used for MVC style programming
  '  */
  class BaseScreen extends mv.BaseView

    'field to allow for convenient tracking of a view
    @alwaysnotify
    public state as string

    function new(name as string)
      super(name)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ nav support
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    'Note the following methods are proxies to facilitate testing.
    'All of them will call out to the top level scope

    ' /**
    '  * @member push
    '  * @memberof module:BaseViewModel
    '  * @instance
    '  * @function
    '  * @description wrapper for push
    '  * @param {view} the view to push, if this is a string, then it is looked up on m - so you can push views by their id, as well as actual views
    '  */
    protected function push(view as mc.types.node)
      'bs:disable-next-line
      push(view)
    end function

    protected function pop() as mc.types.node
      'bs:disable-next-line
      return(pop())
    end function

    protected function resetNavControllerToRoot()
      'bs:disable-next-line
      resetNavControllerToRoot()
    end function

    protected function getNavController()
      'bs:disable-next-line
      return getNavController()
    end function

    protected function resetNavController(newFirstScreen = invalid as mc.types.node, endIndex = -1 as integer)
      'bs:disable-next-line
      resetNavController(newFirstScreen, endIndex)
    end function

    protected function dismiss(state as string)
      if state <> invalid
        m.state = state
        'This cannot be inferred because this is an abstract node class
        'bs:disable-next-line
        m.top.state = state
      end if

      if m.getTopField("navController") <> invalid
        m.pop()
      end if
    end function

  end class
end namespace