import "pkg:/source/core/Utils.bs"

namespace mc.collections

  ' /**
  '  * @member mapArray
  '  * @memberof module:Collections
  '  * @instance
  '  * @function
  '  * @description returns an array with items mapped by the mapper
  '  * @param {array} array of items to map
  '  * @param {class} mapper, function (transform):outputType, or class with a transform(item):outputType function
  '  * @returns {outputType} items mapped by the mapper
  '  */
  function mapArray(array as mc.types.array, mapper as mc.collections.BaseMapper)
    items = []
    if array <> invalid
      isClass = mc.isClass(mapper)
      for each item in array
        if isClass
          transformed = mapper.transform(item)
          if transformed <> invalid
            items.push(transformed)
          end if
        else
          transformed = mapper(item)
          if transformed <> invalid
            items.push(transformed)
          end if
        end if
      end for
    end if
    return items
  end function

  function getSortedArrayByKey(arr as mc.types.array, keyName as string, ascending = true as boolean) as mc.types.array
    result = []
    if mc.isArray(arr) and mc.isString(keyName) and keyName <> ""
      dict = {}
      for each item in arr
        key = item[keyName]
        if mc.isString(key)
          dict[key] = item
        end if
      end for
      keys = dict.keys()
      if not ascending
        keys.reverse()
      end if
      for each key in keys
        result.push(dict[key])
      end for
    end if
    return result
  end function

  function getItemAtOffset(array as mc.types.array, item as dynamic, offset as integer, key = invalid as string)
    if array = invalid or array.count() = 0
      return invalid
    end if
    index = mc.collections.getArrayIndex(array, item, key)
    return array[mc.clamp(index + offset, 0, array.count() - 1)]
  end function

  function getArrayIndex(array as mc.types.array, item as dynamic, key = invalid as string)
    if array = invalid or array.count() = 0
      return -1
    end if

    itemKey = invalid
    isNode = false
    if key <> invalid
      if item <> invalid
        itemKey = item[key]
      else
        return -1
      end if
    else if type(item) = "roAssociativeArray"
      'use id as default key
      key = "id"
      itemKey = item[key]
    else if type(item) = "roSGNode"
      isNode = true
    end if

    for i = 0 to array.count() - 1
      if isNode
        if item.isSamenode(array[i])
          return i
        end if
      else if itemKey <> invalid
        if array[i] <> invalid and array[i][key] = itemKey
          return i
        end if
      else if array[i] = item
        return i
      end if
    end for
    return -1
  end function


  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ mappers
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  class BaseMapper
    function transform(item as dynamic)
    end function
  end class

  class CallFuncMapper extends mc.collections.BaseMapper
    private funcName as string
    private args as mc.types.assocarray

    function new(funcName as string, args as mc.types.assocarray)
      super()
      m.funcName = funcName
      m.args = args
    end function

    override function transform(item as dynamic)
      if item = invalid
        return invalid
      end if
      return item.callFunc(m.funcName, m.args)
    end function
  end class

  class FuncMapper extends mc.collections.BaseMapper
    private funcName as string

    function new(funcName as string)
      super()
      m.funcName = funcName
    end function

    override function transform(item as dynamic)
      if item = invalid
        return invalid
      end if
      if item[m.funcName] = invalid
        return invalid
      end if
      return item[m.funcName]()
    end function
  end class

  class FieldMapper extends mc.collections.BaseMapper
    private fieldName as string

    function new(fieldName as string)
      super()
      m.fieldName = fieldName
    end function

    override function transform(item as dynamic)
      if item = invalid
        return invalid
      end if
      return item[m.fieldName]
    end function
  end class

  ' /**
  '  * @member filterArray
  '  * @memberof module:Collections
  '  * @instance
  '  * @function
  '  * @description returns an array with items that match the filter predicate
  '  * @param {array} array of items to filter
  '  * @param {class} predicate, function (item):bool, or class with a isMatch(item):bool function
  '  * @returns {boolean} items that match the predicate
  '  */
  function filterArray(array as mc.types.array, predicate as dynamic)
    items = []
    if array <> invalid
      isClass = mc.isClass(predicate)
      for each item in array
        if (isClass and predicate.isMatch(item)) or (not isClass and predicate(item))
          items.push(item)
        end if
      end for
    end if
    return items
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ filter predicates
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  class AbstractPredicate
    private expected as dynamic

    function isMatch(expected as dynamic) as boolean
      return false
    end function
  end class

  class ValuePredicate extends mc.collections.AbstractPredicate
    function new(expected as dynamic)
      super(expected)
    end function

    override function isMatch(item as dynamic) as boolean
      return item = m.expected
    end function
  end class

  class FieldPredicate extends mc.collections.AbstractPredicate
    private fieldName as string

    function new(fieldName as string, expected as dynamic)
      super(expected)
      m.fieldName = fieldName
    end function

    override function isMatch(item as dynamic) as boolean
      if item = invalid
        return false
      end if
      return item[m.fieldName] = m.expected
    end function
  end class

  class FuncPredicate extends mc.collections.AbstractPredicate
    private funcName as string

    function new(funcName as string, expected as dynamic)
      super(expected)
      m.funcName = funcName
    end function

    override function isMatch(item as dynamic) as boolean
      if item = invalid
        return false
      end if
      if item[m.funcName] = invalid
        return false
      end if
      compareTo = item[m.funcName]()
      return compareTo = m.expected
    end function
  end class

  class CallFuncPredicate extends mc.collections.AbstractPredicate
    private funcName as string
    private args as mc.types.assocarray

    function new(funcName as string, args as mc.types.assocarray, expected as dynamic)
      super()
      m.funcName = funcName
      m.args = args
    end function

    override function isMatch(item as dynamic) as boolean
      if item = invalid
        return false
      end if
      compareTo = item.callFunc(m.funcName, m.args)
      return m.expected = compareTo
    end function
  end class

  function arrayContains(array as mc.types.array, value as dynamic) as boolean
    return mc.collections.getArrayIndex(array, value) <> -1
  end function

end namespace
