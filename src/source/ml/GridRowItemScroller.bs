import "pkg:/source/ml/ItemScroller.bs"
import "pkg:/source/ml/GridRowItemViewManager.bs"
import "pkg:/source/ml/ItemFocusManager.bs"

namespace ml
  class GridRowItemScroller extends ml.ItemScroller

    public currentRowIndex = 0

    function new(viewManager as ml.GridRowItemViewManager, focusManager as ml.ItemFocusManager)
      super("GridRowItemScroller", viewManager, focusManager)
      m.isOnlyFloatingFocus = true
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ overridden
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    override function getFocusSettings()
      return ml.listMixin.getFocusSettings(m.viewManager.owner.content, m.viewManager.content)
    end function

    'For a grid row, we can only move to the end of the current row
    override function moveToEnd(direction as integer, animated = true as boolean, fast = false as boolean) as boolean

      m.isScrollCancelled = false

      if (m.index >= m.viewManager.numberOfItems - 1 and direction = 1) or m.index = 0 and direction = -1
        m.log.info("already at end - ignoring")
        return false
      end if

      if direction = -1
        index = fix(m.index / m.viewManager.numCols) * m.viewManager.numCols
        m.moveToIndex(index, animated, fast, -1, true)
      else
        index = ((fix(m.index / m.viewManager.numCols) * m.viewManager.numCols) + m.viewManager.numCols) - 1
        m.moveToIndex(index, animated, fast, 1, true)
      end if
      return true
    end function

    protected override function setComponentInfo(info as ml.ComponentInfo)
      super.setComponentInfo(info)
      if info <> invalid
        m.currentRowIndex = fix(info.index / m.viewManager.numCols)
      else
        m.currentRowIndex = -1
      end if
    end function

    protected override function getCellComponentInfo(componentInfo as ml.ComponentInfo, isFromListScroll = false as boolean) as ml.ComponentInfo
      if componentInfo = invalid
        return invalid
      end if
      if isFromListScroll
        ' ? " GRID CHOOSING INDEX CURRENT INDEX IS " ; m.index ; " CI  " ;componentInfo.componentIndex ; " is List scroll? "; isFromListScroll
        colIndex = m.index mod m.viewManager.numCols
        cellIndex = mc.clamp((componentInfo.componentIndex * m.viewManager.numCols) + colIndex, 0, m.numberOfComponents - 1)
        ' ? " GRID CHOSE " ; cellIndex ; " M " ; m.numberOfComponents - 1
        return m.viewManager.cellInfos[cellIndex]
      else
        return m.viewManager.cellInfos[componentInfo.index]
      end if
    end function

    public override function chooseComponentInfoForScrollingIntoRow(componentInfo as ml.ComponentInfo)
      ' ? " GRID CHOOSE SCROLLING INTO ROW" ; componentInfo.index; " CI "; componentInfo.componentIndex
      componentInfo = m.getCellComponentInfo(componentInfo, true)
      if componentInfo <> invalid
        m.setComponentInfo(componentInfo)
        'bs:disable-next-line
        m.viewManager.row.focusedCellIndex = m.index
        ' ? " GRID CHOICE IS MADE index is now "; m.index
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Focus Indicator
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public override function getFocusIndicatorConfig(componentInfo as ml.ComponentInfo, direction as integer, isFloatingFocus as boolean, isFromListScroll = false as boolean) as ml.FocusIndicatorConfig
      info = m.getCellComponentInfo(componentInfo, isFromListScroll)
      config = super.getFocusIndicatorConfig(componentInfo, direction, isFloatingFocus, isFromListScroll)
      config.gridIndex = info.contentIndex
      return config
    end function

    protected override function getFocusIndicatorTranslation(info as ml.ComponentInfo, componentInfo as ml.ComponentInfo, direction as integer, isFloatingFocus as boolean) as object
      if info = invalid or m.viewManager.settings = invalid
        return [0, 0]
      end if

      if not isFloatingFocus and info.contentIndex > 1 and m.viewManager.isVerticalFlowFocusAllowed
        if direction = -1
          screenPos = m.viewManager.upScreenPosition
        else
          screenPos = m.viewManager.downScreenPosition
        end if
      else
        ' screenPos = info.getScreenPosition(direction)
        screenPos = componentInfo.getScreenPosition(direction)
      end if
      ' screenPos = 150
      contentOffset = m.viewManager.settings.contentOffset

      ' ? " info sp "; info.contentIndex ; " " ; screenPos ; " axes "; m.axes

      ' ? " contentOffset "; formatJson(contentOffset)
      translation = [contentOffset[0], contentOffset[1] + screenPos]
      translation = [contentOffset[0], contentOffset[1] + screenPos]

      headerOffset = m.viewManager.settings.headerSettings.height
      translation[1] += headerOffset

      if isFloatingFocus
        ' ? " is float"
        'replace with actual screen location
        cell = m.viewManager.getRenderer(info.contentIndex)
        'bs:disable-next-line
        bounds = cell.sceneBoundingRect()
        translation = [bounds.x, bounds.y]
      end if

      ' ? " END GRID GET FOCUS INDICATOR TRANS"

      return translation
    end function


  end class
end namespace