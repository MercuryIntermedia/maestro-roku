import "pkg:/components/view/mixin/FocusMixin.bs"
import "pkg:/source/core/BaseClass.bs"
import "pkg:/source/core/Date.bs"
import "pkg:/source/view/Animation.bs"

namespace mv
  ' utility for creating various dialogs. Only works with brighterscript classes, and only allows for one dialog at a time
  class DialogUtil extends mc.BaseClass

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Public Fields
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public dialog
    ' private date = new nba.models.Date()

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Private Fields
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private delegate
    private channelStore
    private callback
    private dialogContainer as mc.types.node
    private text as mc.types.assocarray

    @inject("styleManager")
    private styleManager as mc.types.node

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Initialization
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function new(delegate as dynamic)
      super("DialogUtil")
      ' bundle = m.styleManager@.loadBundle("pkg:/source/lib/DialogUtil.bundle")
      bundle = {
        text: {
          onKeyboardDialogButtonSelected: {
            continue: "Continue",
            clear: "Clear",
            back: "Back"
          }
        }
      }
      m.text = bundle.text
      m.delegate = delegate
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Public Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function showDialog(title as string, message as string, buttonTitles = m.text.showDialog.buttonTitles as mc.types.array, callback = invalid as function, additionalArgs = {} as mc.types.assocarray) as mc.types.node
      if additionalArgs = invalid
        additionalArgs = {}
      end if
      container = m.getDialogContainer()
      dialog = m.createSGNode("Dialog", container, `dialog_${container.getChildCount()}`, [
        {
          title: title
          message: message
          buttonTitles: buttonTitles
      }, additionalArgs])
      m.showDialogScreen(dialog, callback)
      return dialog
    end function

    function showDialogFromJson(json as mc.types.assocarray, callback = invalid as function, additionalArgs = {} as mc.types.assocarray) as mc.types.node
      if additionalArgs = invalid
        additionalArgs = {}
      end if
      container = m.getDialogContainer()
      dialog = m.createSGNode("Dialog", container, `dialog_${container.getChildCount()}`, [
        {
          title: json.title
          message: json.message
          buttonTitles: json.buttonTitles
      }, additionalArgs])
      m.showDialogScreen(dialog, callback)
      return dialog
    end function



    'show any screen as a dialog. Must have a selectedIndex: integer field
    function showDialogScreen(dialog as mc.types.node, callback = invalid as function, lockFocus = true as boolean) as mc.types.node
      container = m.getDialogContainer()
      if not container.getParent().isSameNode(dialog.getParent())
        m.log.info("this dialog is not in the dialog containers, adding it now")
        container.appendChild(dialog)
      end if
      mc.setOrAddNodeField(dialog, "dialogLocksFocus", lockFocus)
      m.observeNodeField(dialog, "selectedIndex", m.onDialogDismiss, "both", true)
      if callback <> invalid
        m.log.info("delegate is observing this dialog")
        m.delegate.observeNodeField(dialog, "selectedIndex", callback, "both", true)
      end if
      'FIXME(gcook): need to add a new feature to maestro's focus manager to allow it to set focus on child of focus-locked view : https://github.com/georgejecook/maestro-roku/issues/108
      if lockFocus
        m.setFocusLocked(dialog, "focused on new dialog")
      else
        m.setFocusLocked(invalid, "this dialog does not lock focus")
        m.setFocus(dialog)
      end if
      return dialog
    end function

    ' show a keyboard dialog - this dialog will callback on m
    '              usage:
    ' @param {string} title, title to show
    ' @param {string} text - text to show
    ' @param {string} callback - function to call with updated text
    ' @param {boolean} isSecure - true if it's a password
    ' @param {array} buttons - buttons to show
    ' @param {string} autoPopulate - (none|login|register)
    function showKeyboardDialog(title as string, text as string, callback as function, isSecure as boolean, buttons as mc.types.array, autoPopulate = "none" as string) as mc.types.node
      m.dialog = createObject("roSGNode", "StandardKeyboardDialog")
      if isSecure
        m.dialog.textEditBox.secureMode = true
      end if
      m.dialog.buttons = buttons

      m.dialog.addFields({
        "isSecure": isSecure
      })

      m.callback = callback

      m.dialog.title = title
      m.dialog.keyColor = "0x878686"
      m.dialog.focusedKeyColor = "#000000"

      m.observeNodeField(m.dialog, "buttonSelected", m.onKeyboardDialogButtonSelected, "both")
      m.observeNodeField(m.dialog, "wasClosed", m.closeDialog, "none")

      m.dialog.text = text
      m.dialog.textEditBox.cursorPosition = len(text)

      mc.getScene().dialog = m.dialog

      return m.dialog
    end function

    function dismissDialog(dialog as mc.types.node)
      if dialog <> invalid
        dialog.selectedIndex = -1
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Private Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Keyboard Dialog Callbacks
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function onKeyboardDialogButtonSelected(selectedButton as integer, dialog as mc.types.node)
      selectedButtonText = dialog.buttons[selectedButton]
      m.log.info("keyboard button selected: ", selectedButtonText, ", text ", dialog.text)
      isClosing = false

      buttons = m.text.onKeyboardDialogButtonSelected

      if selectedButtonText = buttons.password
        dialog.textEditBox.secureMode = not dialog.textEditBox.secureMode
      else if selectedButtonText = buttons.continue
        isClosing = true
        m.delegate.__cb = m.callback
        'bs:disable-next-line
        m.delegate.__cb(dialog.text)
      else if selectedButtonText = buttons.back
        isClosing = true
      else if selectedButtonText = buttons.clear
        dialog.text = ""
      end if

      if isClosing
        m.closeDialog()
      end if
    end function

    private function closeDialog()
      m.log.method("closeDialog")

      if m.dialog <> invalid and m.dialog.close = false
        m.dialog.close = true
        mc.getScene().dialog = invalid
      end if

      m.dialog = invalid
    end function

    private function getDialogContainer() as mc.types.node
      scene = mc.getScene()
      container = scene.findNode("_dialogContainer")

      if container = invalid
        container = m.createSGNode("Group", scene, "_dialogContainer")
      end if
      return container
    end function

    private function setFocus(control as mc.types.node)
      'bs:disable-next-line
      mv.setFocus(control)
    end function

    private function setFocusLocked(control as mc.types.node, reason as string)
      'bs:disable-next-line
      mv.setFocusLocked(control, reason)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Callbacks
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function onGetUserData(data as object)
      m.log.method("onGetUserData")
      m.dialog.text = mc.getPath(data, "email", "")
      m.dialog.textEditBox.cursorPosition = len(m.dialog.text)
      m.channelStore = invalid
      mc.getScene().dialog = m.dialog
    end function

    private function onDialogDismiss(selectedIndex as integer, dialog as mc.types.node)
      m.log.info("onDialogDismiss", selectedIndex, mc.dv(dialog))
      'future enhancement - bare in mind that we could have out of order dialogs in future
      dialogContainer = m.getDialogContainer()
      dialogContainer.removeChild(dialog)
      numDialogs = dialogContainer.getChildCount()

      if numDialogs > 0
        m.log.info("there are more dialogs in the dialog stack")
        dialog = dialogContainer.getChild(numDialogs - 1)
        if dialog.dialogLocksFocus
          m.log.info("this dialog locks focus: locking on it now")
          m.setFocusLocked(dialog, "another dialog is still present")
        else
          m.setFocusLocked(invalid, "this dialog does not lock focus")
          m.setFocus(dialog)
        end if
      else
        m.log.info("there are no more dialog. removing dialog stack")
        m.setFocusLocked(invalid, "all dialogs dismissed")
        mc.getScene().removeChild(dialogContainer)
      end if
    end function
  end class
end namespace