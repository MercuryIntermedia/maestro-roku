import "pkg:/source/ml/ListMixin.bs"
import "pkg:/source/view/BaseScreen.bs"
import "pkg:/source/ml/ListDelegateMixin.bs"
import "pkg:/source/view/controls/Styles.bs"
import "pkg:/source/core/Utils.bs"
import "pkg:/source/view/Animation.bs"

@node("ButtonScreen", "mv_BaseScreen")
class ButtonScreen extends mv.BaseScreen

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Views
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private controlsGroup as mc.types.node
  private button1 as mc.types.node
  private button2 as mc.types.node
  private button3 as mc.types.node
  private button4 as mc.types.node
  private button5 as mc.types.node
  private animButton as mc.types.node

  private viewJson = [{
    id: "animButton"
    _type: "mv_Button"
    text: "longer text"
    translation: [100, 100]
    size: [400, 60]
    horizAlign: "left"
    style: mv.styles.getDefaultButtonStyle()
  }]

  private updatedViewJson = [{
    id: "animButton"
    text: "small"
    translation: [1800, 600]
    size: [200, 60]
    horizAlign: "left"
    style: m.setStyleColor(mv.styles.getDefaultButtonStyle(), "#aa0000", "#ff0000", "#aa0000", "#000000", "#ffffff", "#aaaaaa")
  }]

  private updatedViewJson2 = [{
    id: "animButton"
    text: "red"
    translation: [400, 100]
    size: [800, 100]
    horizAlign: "center"
    style: m.setStyleColor(mv.styles.getDefaultButtonStyle(), "#aa0000", "#ff0000", "#aa0000", "#000000", "#ffffff", "#aaaaaa")

  }]

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Animations
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private buttonAnim as mv.animations.ViewJson

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Fields
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  @sync
  @observer("onTitleChange")
  @inject("user", "settings.spoilers")
  private title as string

  function new()
    super("ButtonScreen")
  end function

  protected override function initialize()
    super.initialize()
    m.createViews()
    m.buttonAnim = new mv.animations.ViewJson(m.viewJson, m.updatedViewJson, 5)
    m.buttonAnim.start()
    m.observeNodeField(m.controlsGroup, "selectedIndex", m.onSelectButton)
  end function

  private function createViews()
    mv.createViewsFromStyleJson(m.viewJson, m.top)

    redButtonStyle = mv.styles.getDefaultButtonStyle()
    m.setStyleColor(redButtonStyle, "#aa0000", "#ff0000", "#aa0000", "#000000", "#ffffff", "#aaaaaa")

    blueButtonStyle = mv.styles.getDefaultButtonStyle()
    m.setStyleColor(blueButtonStyle, "#0000aa", "#0000ff", "#0000aa", "#000000", "#ffffff", "#0000aa")

    m.controlsGroup = m.createSGNode("mv_ControlsGroup", m.top, "controlsGroup", {
      translation: [200, 200]
      layoutMode: "vert"
      keyDirection: "vert"
      itemSpacings: [10]
    })

    m.button1 = m.createSGNode("mv_Button", m.controlsGroup, "button1", {
      ' text: "simple"
      text: `spoilers: ${m.title}`
      style: redButtonStyle
    })

    m.styleManager@.addStyle("buttons", mc.createPath("blue", blueButtonStyle))

    iconStyle = {
      "blendColor": "#ffffff"
      "size": [48, 48]
      "translation": [5, 0]
      uri: "pkg:/images/small-star-$$RES$$.png"
    }

    m.button2 = m.createSGNode("mv_Button", m.controlsGroup, "button1", {
      text: "left + icon"
      iconStyle: iconStyle
    })

    m.button3 = m.createSGNode("mv_Button", m.controlsGroup, "button3", {
      text: "autosize text right"
      style: redButtonStyle
      autoSize: true
      horizAlign: "right"
    })
    m.button3.iconStyle = iconStyle

    m.button4 = m.createSGNode("mv_Button", m.controlsGroup, "button4", {
      text: "autosize text that grows left"
      horizAlign: "left"
      autoSize: true
      iconStyle: iconStyle
    })
    m.button5 = m.createSGNode("mv_Button", m.controlsGroup, "button5", {
      text: ""
      size: [50, 50]
      autoSize: true
      horizAlign: "center"
      styleKey: "buttons.blue"
      iconStyle: iconStyle
    })
    m.button5 = m.createSGNode("mv_Button", m.controlsGroup, "button5", {
      text: "autosize really really really really really long text"
      autoSize: true
      horizAlign: "left"
      iconStyle: iconStyle
    })
    m.button5 = m.createSGNode("mv_Button", m.controlsGroup, "button5", {
      text: "short"
      autoSize: true
      size: [50, 50]
      horizAlign: "left"
      iconStyle: iconStyle
    })
    m.controlsGroup@.updateChildren()
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Methods
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function onTitleChange(value as string)
    m.button1.text = `spoilers: ${m.title}`
  end function

  private function setStyleColor(buttonStyle as mc.types.assocarray, bgn as string, bgf as string, bgs as string, ln as string, lf as string, ls as string)
    mc.setPath(buttonStyle, "normal.backgroundPoster.blendColor", bgn)
    mc.setPath(buttonStyle, "focused.backgroundPoster.blendColor", bgf)
    mc.setPath(buttonStyle, "selected.backgroundPoster.blendColor", bgs)
    mc.setPath(buttonStyle, "normal.label.color", ln)
    mc.setPath(buttonStyle, "focused.label.color", lf)
    mc.setPath(buttonStyle, "selected.label.color", ls)
    return buttonStyle
  end function

  private function onSelectButton(index as integer)
    m.log.info("selected button", index)
    if index = 0
      ? "PAUSE"
      m.buttonAnim.pause()
    else if index = 1
      ? "RESTART"
      m.buttonAnim.start()
    else if index = 2
      ? "CONTINUE"
      m.buttonAnim.start(true)
    else if index = 3
      ? "CHANGE 2"
      m.buttonAnim.animateTo(m.updatedViewJson2)
      m.buttonAnim.start(true)
    else if index = 4
      ? "CHANGE 1"
      m.buttonAnim.animateTo(m.updatedViewJson)
      m.buttonAnim.start(true)
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Key Presses
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function onKeyPressUp() as boolean
    ? "OK UP"
    return false
  end function


  private function onKeyPressLeft() as boolean
    ? "OK LEFT"
    return false
  end function
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Lifecycle
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  protected override function onGainedFocus(isSelfFocused as boolean)
    super.onGainedFocus(isSelfFocused)

    if isSelfFocused
      m.setFocus(m.controlsGroup)
    end if
  end function

end class
