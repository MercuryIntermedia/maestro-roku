import "pkg:/source/view/BaseScreen.bs"
import "pkg:/source/core/Utils.bs"

@node("NavControllerTestScreen", "mv_BaseScreen")
class NavControllerTestScreen extends mv.BaseScreen

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Fields
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  private bgRect as mc.types.node
  private navController as mc.types.node

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Initialization
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  function new()
    super("NavControllerTestScreen")
  end function

  protected override function initialize()
    m.log.info("Initialized NavControllerTestScreen")
    super.initialize()

    m.bgRect = m.createSGNode("mv_Rectangle", m.top, "bgRect", {
      size: [1920, 1080]
      color: "#b2f3b2"
    })

    m.navController = m.createSGNode("mv_navController", m.top, "navController")
    transition = m.createSGNode("mv_FadeTransition", m.top, "fadeTransition", {
      color: "#ff0000"
    })
    m.navController.transition = transition
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Key Presses
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  protected override function onKeyPressLeft() as boolean
    m.log.info("Pushing a screen")
    childScreen = m.createScreen("NavControllerTestChildScreen", "NavControllerTestChildScreen")
    m.navController@.push(childScreen)
    return true
  end function

  protected override function onKeyPressRight() as boolean
    m.log.info("Popping a screen")
    m.navController@.pop()
    return true
  end function

  private function onKeyPressDown() as boolean
    shown = not m.navController.visible
    m.log.info("toggling navcontroller visibility", shown)
    m.navController@._setShown(shown)
    return true
  end function

end class
